#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

piece  base, body, nose, aft1, aft3, turret1, turret2,
		msl11, msl12, msl13, msl14, msl15, msl16,
		msl21, msl22, msl23, msl24, msl25, msl26,
       wake1, wake2;

static-var  restore_delay, gun_1, gun_2;

// Signal definitions
#define SIG_AIM_1			2
#define SIG_AIM_2			4
#define SIG_MOVE			8

waveSplash()
{
	while( TRUE )
	{
		turn base to x-axis <-3.000000> speed <3.000000>;
		move base to y-axis [0.319995] speed [0.319995];
		wait-for-turn base around x-axis;
		turn base to x-axis <0.000000> speed <3.000000>;
		move base to y-axis [0.000000] speed [0.319995];
		wait-for-turn base around x-axis;
		sleep 150;
	}
}

SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 500;
	}
	
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 258;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 257;
			}
			emit-sfx smoketype from base;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
}

Create()
{
	restore_delay = 2500;
	gun_1 = 0;
	gun_2 = 0;
	start-script SmokeUnit();
}

RestoreAfterDelay()
{
	sleep restore_delay;
	turn turret1 to y-axis <0.000000> speed <75.000000>;
	turn turret1 to x-axis <0.00> speed <75.00>;
	move turret1 to y-axis [0.00] speed [1.00];	
	
	turn turret2 to y-axis <0.000000> speed <75.000000>;
	turn turret2 to x-axis <0.00> speed <75.00>;
	move turret2 to y-axis [0.00] speed [1.00];	
}

StartMoving()
{
	signal SIG_MOVE;
	set-signal-mask SIG_MOVE;
	start-script waveSplash();
	while( TRUE )
	{
		emit-sfx 2 from wake1;
		emit-sfx 2 from wake2;
		sleep 150;
	}
}

StopMoving()
{
	signal SIG_MOVE;
	turn base to x-axis <0.000000> speed <3.000000>;
	move base to y-axis [0.000000] speed [0.319995];
}

AimFromPrimary(pieceNum)
{
	pieceNum = turret1;
}

AimFromSecondary(pieceNum)
{
	pieceNum = turret2;
}

AimPrimary(heading, pitch)
{
	signal SIG_AIM_1;
	set-signal-mask SIG_AIM_1;
	move turret1 to y-axis [2.50] speed [15.00];
	turn turret1 to y-axis heading speed <360.00>;
	turn turret1 to x-axis <0.00> - pitch speed <320.00>;	
	wait-for-turn turret1 around y-axis;
	wait-for-turn turret1 around x-axis;
	wait-for-move turret1 along y-axis;
	start-script RestoreAfterDelay();
	return (1);
}

AimSecondary(heading, pitch)
{
	signal SIG_AIM_2;
	set-signal-mask SIG_AIM_2;
	move turret2 to y-axis [2.50] speed [15.00];
	turn turret2 to y-axis heading speed <360.00>;
	turn turret2 to x-axis <0.00> - pitch speed <320.00>;	
	wait-for-turn turret2 around y-axis;
	wait-for-turn turret2 around x-axis;
	wait-for-move turret2 along y-axis;
	start-script RestoreAfterDelay();
	return (1);
}

Shot1()
{
	++gun_1;
	if (gun_1 == 6)
	{
		gun_1 = 0;
	}
}

Shot2()
{
	++gun_2;
	if (gun_2 == 6)
	{
		gun_2 = 0;
	}
}

QueryPrimary(pieceNum)
{
	if (gun_1 == 0)
	{
		pieceNum = msl11;
	}
	if (gun_1 == 1)
	{
		pieceNum = msl12;
	}
	if (gun_1 == 2)
	{
		pieceNum = msl13;
	}
	if (gun_1 == 3)
	{
		pieceNum = msl14;
	}
	if (gun_1 == 4)
	{
		pieceNum = msl15;
	}
	if (gun_1 == 5)
	{
		pieceNum = msl16;
	}
}

QuerySecondary(pieceNum)
{
	if (gun_2 == 0)
	{
		pieceNum = msl21;
	}
	if (gun_2 == 1)
	{
		pieceNum = msl22;
	}
	if (gun_2 == 2)
	{
		pieceNum = msl23;
	}
	if (gun_2 == 3)
	{
		pieceNum = msl24;
	}
	if (gun_2 == 4)
	{
		pieceNum = msl25;
	}
	if (gun_2 == 5)
	{
		pieceNum = msl26;
	}
}

SweetSpot(piecenum)
{
	piecenum = base;
}

Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1;
		explode aft1 type NOHEATCLOUD;
		explode nose type NOHEATCLOUD;
		explode body type NOHEATCLOUD;
		explode turret1 type NOHEATCLOUD;
		explode turret2 type NOHEATCLOUD;
		return (corpsetype);
	}
	if( severity <= 50 )
	{
		corpsetype = 2;
		explode aft1 type NOHEATCLOUD | FALL;
		explode nose type NOHEATCLOUD | FALL;
		explode body type NOHEATCLOUD;
		explode turret1 type NOHEATCLOUD;
		explode turret2 type NOHEATCLOUD;
		return (corpsetype);
	}
	corpsetype = 3;
	explode aft1 type NOHEATCLOUD | FALL | SMOKE | FIRE | EXPLODE_ON_HIT;
	explode nose type NOHEATCLOUD | FALL | SMOKE | FIRE | EXPLODE_ON_HIT;
	explode body type NOHEATCLOUD;
	explode turret1 type NOHEATCLOUD | SHATTER | EXPLODE_ON_HIT;
	explode turret2 type NOHEATCLOUD | SHATTER | EXPLODE_ON_HIT;
	return (corpsetype);
}
