#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

piece  base, body, tower, wheel1, wheel2, wheel3, wheel4,
		wake1, wake2, wake3, wake4, radar,
		turret1, sleeve11, sleeve12, flare11, flare12, barrel11, barrel12, 
		turret2, sleeve21, sleeve22, flare21, flare22, barrel21, barrel22;

static-var  gun_1, gun_2, Static_Var_1;

#define SIG_AIM_1 	2
#define SIG_AIM_2 	4
#define SIG_MOVE 	8

SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 400;
	}
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 256 | 2;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 256 | 1;
			}
			emit-sfx smoketype from body;
			emit-sfx smoketype from turret1;
			emit-sfx smoketype from turret2;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
}

Activate()
{
	spin radar around y-axis speed <100.000000> accelerate <1.000000>;
}

Deactivate()
{
	spin radar around y-axis speed <0.000000> accelerate <1.000000>;
}

Create()
{
	hide flare11;
	hide flare12;
	hide flare21;
	hide flare22;
	gun_1 = 0;
	gun_2 = 0;
	start-script SmokeUnit();	
}

setSFXoccupy(state)
{
	Static_Var_1 = state;
}

StartMoving()
{
	signal SIG_MOVE;
	set-signal-mask SIG_MOVE;
	while( TRUE )
	{
		if( Static_Var_1 == 1 or Static_Var_1 == 2 )
		{
			sleep 100;
			emit-sfx 256 | 3 from wake1;
			sleep 100;
			emit-sfx 256 | 3 from wake2;
			sleep 100;
			emit-sfx 256 | 3 from wake3;
			sleep 100;
			emit-sfx 256 | 3 from wake4;
		}
		
		sleep 125;
	}
}

RestoreAfterDelay1()
{
	sleep 1000;
	spin barrel11 around z-axis speed <0.000000> accelerate <-10.000000>;
	spin barrel12 around z-axis speed <0.000000> accelerate <-10.000000>;
	sleep 2000;
	turn turret1 to y-axis <0.000000> speed <100.000000>;
	turn sleeve11 to x-axis <0.000000> speed <90.000000>;
	turn sleeve12 to x-axis <0.000000> speed <90.000000>;
}

RestoreAfterDelay2()
{
	sleep 1000;
	spin barrel21 around z-axis speed <0.000000> accelerate <-10.000000>;
	spin barrel22 around z-axis speed <0.000000> accelerate <-10.000000>;
	sleep 2000;
	turn turret2 to y-axis <0.000000> speed <100.000000>;
	turn sleeve21 to x-axis <0.000000> speed <90.000000>;
	turn sleeve22 to x-axis <0.000000> speed <90.000000>;
}

AimWeapon1(heading, pitch)
{
	signal SIG_AIM_1;
	set-signal-mask SIG_AIM_1;
	turn turret1 to y-axis heading speed <100.000000>;
	turn sleeve11 to x-axis 0 - pitch speed <100.000000>;
	turn sleeve12 to x-axis 0 - pitch speed <100.000000>;
	wait-for-turn turret1 around y-axis;
	wait-for-turn sleeve11 around x-axis;
	start-script RestoreAfterDelay1();
	return (1);
}

AimWeapon2(heading, pitch)
{
	signal SIG_AIM_2;
	set-signal-mask SIG_AIM_2;
	turn turret2 to y-axis heading speed <100.000000>;
	turn sleeve21 to x-axis 0 - pitch speed <100.000000>;
	turn sleeve22 to x-axis 0 - pitch speed <100.000000>;
	wait-for-turn turret2 around y-axis;
	wait-for-turn sleeve22 around x-axis;
	start-script RestoreAfterDelay2();
	return (1);
}

FireWeapon1()
{
	if(gun_1 == 0)
	{
		spin barrel11 around z-axis speed <425.000000>;	
	}
	if(gun_1 == 1)
	{
		spin barrel12 around z-axis speed <425.000000>;	
	}
	++gun_1;
	if(gun_1 == 2)
	{
		gun_1 = 0;
	}
}

FireWeapon2()
{
	if(gun_2 == 0)
	{
		spin barrel21 around z-axis speed <425.000000>;	
	}
	if(gun_2 == 1)
	{
		spin barrel22 around z-axis speed <425.000000>;	
	}
	++gun_2;
	if(gun_2 == 2)
	{
		gun_2 = 0;
	}
}

AimFromWeapon1(piecenum)
{
	piecenum = turret1;
}

AimFromWeapon2(piecenum)
{
	piecenum = turret2;
}


QueryWeapon1(piecenum)
{
	if(gun_1 == 0)
	{
		piecenum = flare11;
	}
	if(gun_1 == 1)
	{
		piecenum = flare12;
	}
}

QueryWeapon2(piecenum)
{
	if(gun_2 == 0)
	{
		piecenum = flare21;
	}
	if(gun_2 == 1)
	{
		piecenum = flare22;
	}
}

SweetSpot(piecenum)
{
	piecenum = base;
}

Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1 ;
		explode body type NOHEATCLOUD ;
		explode turret1 type NOHEATCLOUD;
		explode turret2 type NOHEATCLOUD;
		explode barrel11 type NOHEATCLOUD;
		explode barrel22 type NOHEATCLOUD;
		explode wheel1 type NOHEATCLOUD;
		explode wheel2 type NOHEATCLOUD;
		explode tower type NOHEATCLOUD;
		explode radar type NOHEATCLOUD | FALL | SMOKE | FIRE | EXPLODE_ON_HIT;
		return (corpsetype);
	}
	if( severity <= 50 )
	{
		corpsetype = 2;
		explode body type FALL | NOHEATCLOUD;
		explode turret1 type FALL | NOHEATCLOUD;
		explode turret2 type FALL | NOHEATCLOUD;
		explode barrel11 type FALL | NOHEATCLOUD;
		explode barrel22 type FALL | NOHEATCLOUD;
		explode wheel1 type NOHEATCLOUD;
		explode wheel2 type NOHEATCLOUD;
		explode wheel3 type NOHEATCLOUD;
		explode wheel4 type NOHEATCLOUD;
		explode tower type SHATTER | NOHEATCLOUD;
		explode radar type NOHEATCLOUD | FALL | SMOKE | FIRE | EXPLODE_ON_HIT;
		return (corpsetype);
	}
	corpsetype = 3;
	explode body type NOHEATCLOUD;
	explode turret1 type NOHEATCLOUD | FALL | SMOKE | FIRE | EXPLODE_ON_HIT;
	explode turret2 type NOHEATCLOUD | FALL | SMOKE | FIRE | EXPLODE_ON_HIT;
	explode barrel11 type NOHEATCLOUD | FALL | SMOKE | FIRE | EXPLODE_ON_HIT;
	explode barrel22 type NOHEATCLOUD | FALL | SMOKE | FIRE | EXPLODE_ON_HIT;
	explode wheel1 type NOHEATCLOUD | FALL | SMOKE | FIRE | EXPLODE_ON_HIT;
	explode wheel2 type NOHEATCLOUD | FALL | SMOKE | FIRE | EXPLODE_ON_HIT;
	explode wheel3 type NOHEATCLOUD | FALL | SMOKE | FIRE | EXPLODE_ON_HIT;
	explode wheel4 type NOHEATCLOUD | FALL | SMOKE | FIRE | EXPLODE_ON_HIT;
	explode radar type NOHEATCLOUD | FALL | SMOKE | FIRE | EXPLODE_ON_HIT;
	explode tower type SHATTER | NOHEATCLOUD;
	return (corpsetype);
}
