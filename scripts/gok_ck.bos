#define TA

#include "sfxtype.h"
#include "exptype.h"

piece  base, pelvis, lleg, lboot, lfoot, rleg, rboot, rfoot, torso, pitchtorso, head, torsorecoil,
       lshoulder, lshoulderrecoil, larm, larmrecoil, rshoulder, rshoulderrecoil, rarm, rarmrecoil, beam1, beam2,
       loin1, loin1a, loin1b, loin1c;

static-var  bMoving, bAiming, nano;

MotionControl(Moving, Aiming, JustMoved)
{
	JustMoved = 1;
	while( TRUE )
		{
		Moving = bMoving;
		Aiming = bAiming;
		if( Moving )
			{
			if( bMoving )
				{
				turn lleg to x-axis <30.00> speed <100.00>;
				turn lboot to x-axis <-10.00> speed <40.00>;
				turn lfoot to x-axis <-20.00> speed <80.00>;
				turn rleg to x-axis <-60.00> speed <240.00>;
				turn rboot to x-axis <100.00> speed <40.00>;
				turn rfoot to x-axis <0.00> speed <0.00>;
				move pelvis to y-axis [1.00] speed [6.00];
				turn loin1 to x-axis <20.00> speed <20.00>;
				if( !bAiming )
					{
					turn lshoulder to x-axis <25.00> speed <40.00>;
					turn larm to x-axis <40.00> speed <13.33>;
					turn rshoulder to x-axis <-5.00> speed <30.00>;
					turn torso to y-axis <-10.00> speed <40.00>;
					turn torsorecoil to x-axis <5.00> speed <20.00>;
					}				
				sleep 250;
				}
			
			if( bMoving )
				{
				turn lleg to x-axis <50.00> speed <80.00>;
				turn lboot to x-axis <20.00> speed <120.00>;
				turn lfoot to x-axis <10.00> speed <120.00>;
				turn rleg to x-axis <-40.00> speed <80.00>;
				turn rboot to x-axis <10.00> speed <360.00>;
				turn rfoot to x-axis <20.00> speed <80.00>;
				move pelvis to y-axis [-1.00] speed [8.00];
				turn loin1 to x-axis <30.00> speed <40.00>;
				if( !bAiming )
					{
					turn torsorecoil to x-axis <0.00> speed <20.00>;
					}				
				sleep 250;
				}
			
			if( bMoving )
				{
				turn lleg to x-axis <0.00> speed <200.00>;
				turn lboot to x-axis <90.00> speed <280.00>;
				turn lfoot to x-axis <0.00> speed <40.00>;
				turn rleg to x-axis <5.00> speed <180.00>;
				turn rboot to x-axis <0.00> speed <40.00>;
				turn rfoot to x-axis <0.00> speed <80.00>;
				move pelvis to y-axis [-0.50] speed [2.00];
				turn loin1 to x-axis <15.00> speed <30.00>;
				if( !bAiming )
					{
					turn lshoulder to x-axis <35.00> speed <40.00>;
					turn larm to x-axis <45.00> speed <20.00>;
					turn rshoulder to x-axis <20.00> speed <30.00>;
					turn rarm to x-axis <45.00> speed <60.00>;
					turn torso to y-axis <0.00> speed <40.00>;
					turn torsorecoil to x-axis <10.00> speed <40.00>;
					}
				sleep 250;
				}
			
			if( bMoving )
				{
				turn lleg to x-axis <-60.00> speed <240.00>;
				turn lboot to x-axis <100.00> speed <40.00>;
				turn lfoot to x-axis <0.00> speed <0.00>;
				turn rleg to x-axis <30.00> speed <100.00>;
				turn rboot to x-axis <-10.00> speed <40.00>;
				turn rfoot to x-axis <-20.00> speed <80.00>;
				move pelvis to y-axis [1.00] speed [6.00];
				turn loin1 to x-axis <20.00> speed <20.00>;
				if( !bAiming )
					{
					turn lshoulder to x-axis <45.00> speed <40.00>;
					turn larm to x-axis <50.00> speed <13.33>;
					turn rshoulder to x-axis <55.00> speed <30.00>;
					turn rarm to x-axis <15.00> speed <60.00>;
					turn torso to y-axis <-10.00> speed <40.00>;
						turn torsorecoil to x-axis <5.00> speed <20.00>;
					}			
				sleep 250;
				}
			
			if( bMoving )
				{
				turn lleg to x-axis <-40.00> speed <80.00>;
				turn lboot to x-axis <10.00> speed <360.00>;
				turn lfoot to x-axis <20.00> speed <80.00>;
				turn rleg to x-axis <50.00> speed <80.00>;
				turn rboot to x-axis <20.00> speed <120.00>;
				turn rfoot to x-axis <10.00> speed <120.00>;
				move pelvis to y-axis [-1.00] speed [8.00];
				turn loin1 to x-axis <30.00> speed <40.00>;
				if( !bAiming )
					{
					turn torsorecoil to x-axis <0.00> speed <20.00>;
					}			
				sleep 250;
				}
			
			if( bMoving )
				{
				turn lleg to x-axis <5.00> speed <180.00>;
				turn lboot to x-axis <0.00> speed <40.00>;
				turn lfoot to x-axis <0.00> speed <80.00>;
				turn rleg to x-axis <0.00> speed <200.00>;
				turn rboot to x-axis <90.00> speed <280.00>;
				turn rfoot to x-axis <0.00> speed <40.00>;
				move pelvis to y-axis [-0.50] speed [2.00];
				turn loin1 to x-axis <0.00> speed <30.00>;
				if( !bAiming )
					{
					turn lshoulder to x-axis <35.00> speed <40.00>;
					turn larm to x-axis <45.00> speed <20.00>;
					turn rshoulder to x-axis <15.00> speed <30.00>;
					turn rarm to x-axis <45.00> speed <60.00>;
					turn torso to y-axis <10.00> speed <40.00>;				
					turn torsorecoil to x-axis <10.00> speed <40.00>;
					}
				sleep 250;
				}
			JustMoved = 1;
			}
		if( !Moving )
			{
			if( JustMoved )
				{
				turn lleg to x-axis <0.00> speed <180.00>;
				turn lboot to x-axis <0.00> speed <180.00>;
				turn lfoot to x-axis <0.00> speed <180.00>;
				turn rleg to x-axis <0.00> speed <180.00>;
				turn rboot to x-axis <0.00> speed <180.00>;
				turn rfoot to x-axis <0.00> speed <180.00>;
				move pelvis to y-axis [0.00] speed [10.00];
				turn pelvis to x-axis <0.00> speed <10.00>;
				turn torsorecoil to x-axis <0.00> speed <10.00>;
				turn loin1 to x-axis <0.00> speed <20.00>;
				if( !Aiming )
					{
					turn lshoulder to x-axis <0.00> speed <150.00>;
					turn larm to x-axis <90.00> speed <50.00>;					
					turn rshoulder to x-axis <0.00> speed <30.00>;
					turn rarm to x-axis <90.00> speed <150.00>;			
					turn torso to y-axis <-10.00> speed <100.00>;
					}
				JustMoved = 0;
				}
			sleep 100;
			}
		}
}

SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 400;
	}
	
	start-script MotionControl();
	
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 256 | 2;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 256 | 1;
			}
			emit-sfx smoketype from torso;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
}

Create()
{
	bMoving = 0;
	bAiming = 0;	
	turn lshoulder to x-axis <-15.00> now;
	turn larm to x-axis <25.00> now;
	turn rshoulder to x-axis <-15.00> now;
	turn rarm to x-axis <25.00> now;
	start-script SmokeUnit();
}

StartMoving()
{
	signal 4;
	bMoving = 1;
	bAiming = 0;
}

StopMoving()
{
	set-signal-mask 4;
	bMoving = 0;
	
	sleep 700;
	
	move pelvis to y-axis [-0.0] speed [1.25];
	move pelvis to z-axis [-0.00] speed [2.50];
	
	turn lleg to x-axis <-0.00> speed <75.00>;
	turn lboot to x-axis <0.00> speed <112.50>;
	turn lfoot to x-axis <-0.00> speed <37.50>;
	
	turn rleg to x-axis <0.00> speed <50.00>;
	turn rboot to x-axis <0.00> speed <175.00>;
	turn rfoot to x-axis <-0.00> speed <150.00>;
	
	turn loin1 to x-axis <-0.00> speed <25.00>;
	
	sleep 200;
	
	turn rleg to x-axis <0.00> speed <25.00>;
	turn rboot to x-axis <0.00> speed <100.00>;
	//turn rfoot to x-axis <-30.00> speed <0.00>;
	
	sleep 200;
	
	move pelvis to y-axis [-0.0] speed [0.625];
	move pelvis to z-axis [0.00] speed [2.50];
	
	turn lleg to x-axis <-0.00> speed <50.00>;
	turn lboot to x-axis <0.00> speed <100.00>;
	turn lfoot to x-axis <-0.00> speed <25.00>;
	
	turn rleg to x-axis <0.00> speed <12.50>;
	turn rboot to x-axis <0.00> speed <0.00>;
	turn rfoot to x-axis <0.00> speed <12.50>;
	
	turn loin1 to x-axis <10.00> speed <50.00>;
	
	sleep 200;
	
	//turn lleg to x-axis <-40.00> speed <0.00>;
	//turn lboot to x-axis <50.00> speed <75.00>;
	//turn lfoot to x-axis <-10.00> speed <50.00>;
	
	sleep 200;//1500
	
	turn loin1 to x-axis <0.00> speed <25.00>;
}

SweetSpot(PieceNum)
{
	PieceNum = base;
}

QueryNanoPiece(piecenum)
{
	if( nano == 0 )
	{
		piecenum = beam1;
	}
	if( nano == 1 )
	{
		piecenum = beam2;
	}
	nano = !nano;
}

RestorePosition()
{
	turn torso to y-axis <0.000000> speed <90.000000>;
	turn head to y-axis <0.000000> speed <45.000000>;
	wait-for-turn torso around y-axis;
}

StartBuilding(Func_Var_1, Func_Var_2)
{
	turn larm to x-axis <0.00> speed <90.00>;
	turn rarm to x-axis <0.00> speed <90.00>;
	turn torsorecoil to x-axis <0.00> speed <30.00>;
	turn torso to y-axis Func_Var_1 speed <160.00>;
	turn head to y-axis Func_Var_1 speed <200.00>;
	wait-for-turn rarm around y-axis;
	wait-for-turn torso around y-axis;
	wait-for-turn pitchtorso around x-axis;
	wait-for-turn torsorecoil around y-axis;
	set INBUILDSTANCE to 1;
}

StopBuilding()
{
	set INBUILDSTANCE to 0;
	signal 2;
	set-signal-mask 2;
	call-script RestorePosition();
}

Killed(Severity, CorpseType, Random)
{	
	if( Severity <= 75 )
	{
		CorpseType = 1;
		explode pelvis type NOHEATCLOUD;
		explode head type FALL | NOHEATCLOUD;
		explode rboot type FALL | NOHEATCLOUD;
		explode lshoulder type FALL | NOHEATCLOUD;
		return (CorpseType);
	}
	
	CorpseType = 3;	
	explode pelvis type NOHEATCLOUD;
	explode head type FALL | SMOKE | EXPLODE_ON_HIT | NOHEATCLOUD;
	explode lshoulder type FALL | SMOKE | EXPLODE_ON_HIT | NOHEATCLOUD;
	explode rboot type FALL | SMOKE | EXPLODE_ON_HIT | NOHEATCLOUD;
	return (CorpseType);	
}
