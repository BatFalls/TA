#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

piece  base, ground, flare, body, turret, sleeves, gun, socle, support;

static-var  restore_delay, sleeves_1;

// Signal definitions
#define SIG_AIM				2


SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 400;
	}
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 256 | 2;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 256 | 1;
			}
			emit-sfx smoketype from base;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
}

Create()
{
	hide flare;
	sleeves_1 = 1;
	restore_delay = 2000;
	start-script SmokeUnit();
}

QueryPrimary(piecenum)
{
	piecenum = flare;
}

AimFromPrimary(piecenum)
{
	piecenum = sleeves;
}

SweetSpot(piecenum)
{
	piecenum = base;
}

RestoreAfterDelay()
{
	sleep restore_delay;
	turn turret to y-axis <0.000000> speed <50.0>;
	turn sleeves to x-axis <0.000000> speed <30.0>;
	
}

AimPrimary(heading, pitch)
{
	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	turn turret to y-axis heading speed <120.0>;
	turn sleeves to x-axis <0.000000> - pitch speed <80.0>;
	wait-for-turn turret around y-axis;
	wait-for-turn sleeves around x-axis;
	start-script RestoreAfterDelay();
	return (1);
}

FirePrimary()
{
	emit-sfx 1024 + 0 from flare;
	turn gun to z-axis <60.0> * sleeves_1 speed <700.0>;
	++sleeves_1;
	if( sleeves_1 == 7 )
	{
		sleeves_1 = 1;
	}
}

Killed(severity, corpsetype)
{
	hide flare;
	if( severity <= 25 )
	{
		corpsetype = 1;
		explode ground type NOHEATCLOUD;
		explode turret type NOHEATCLOUD;
		explode sleeves type NOHEATCLOUD;
		explode gun type NOHEATCLOUD;
		explode support type NOHEATCLOUD;
		return (corpsetype);
	}
	if( severity <= 40 )
	{
		corpsetype = 2;
		explode ground type NOHEATCLOUD;
		explode turret type NOHEATCLOUD | FALL;
		explode sleeves type NOHEATCLOUD | FALL;
		explode gun type NOHEATCLOUD | FALL;
		explode support type NOHEATCLOUD | FALL;
		return (corpsetype);
	}
	corpsetype = 3;
	explode ground type NOHEATCLOUD | SMOKE | FIRE;
	explode turret type NOHEATCLOUD | FALL | SMOKE | FIRE | EXPLODE_ON_HIT;
	explode sleeves type NOHEATCLOUD | FALL | SMOKE | FIRE | EXPLODE_ON_HIT;
	explode gun type NOHEATCLOUD | FALL | SMOKE | FIRE | EXPLODE_ON_HIT;
	explode support type NOHEATCLOUD | FALL | SMOKE | FIRE | EXPLODE_ON_HIT;
	return (corpsetype);
}
