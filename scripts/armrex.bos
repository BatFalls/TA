#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

piece  base, turret1, sleeve1, gun1, flare1, turret2, barrelB1, barrelB2,
		canonB1, canonB2, flareB1, flareB2;

static-var  restore_delay, gun_2;

// Signal definitions
#define SIG_AIM				2
#define SIG_AIM_2			4


SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 400;
	}
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 256 | 2;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 256 | 1;
			}
			emit-sfx smoketype from base;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
}

Create()
{
	hide flare1;
	hide flareB1;
	hide flareB2;
	gun_2 = 0;
	restore_delay = 3000;
}

RestoreAfterDelay()
{
	sleep restore_delay;
	turn turret1 to y-axis <0.000000> speed <15.000000>;
	turn sleeve1 to x-axis <0.000000> speed <15.000000>;
}

RestoreAfterDelay2()
{
	sleep restore_delay / 2 ;
	turn turret2 to y-axis <0.000000> speed <50.0>;
	turn canonB1 to x-axis <0.000000> speed <35.0>;
	turn canonB2 to x-axis <0.000000> speed <35.0>;
}

AimPrimary(heading, pitch)
{
	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	turn turret1 to y-axis heading speed <60.000000>;
	turn sleeve1 to x-axis <0.000000> - pitch speed <45.000000>;
	wait-for-turn turret1 around y-axis;
	wait-for-turn sleeve1 around x-axis;
	start-script RestoreAfterDelay();
	return (1);
}

FirePrimary()
{
	//show flare1;
	turn gun1 to z-axis <0.000000> now;
	turn gun1 to z-axis <60.0> speed <250.0>;
	//sleep 150;
	//hide flare1;
}

QueryPrimary(piecenum)
{
	piecenum = flare1;
}

AimFromPrimary(piecenum)
{
	piecenum = turret1;
}


AimFromSecondary(piecenum)
{
	piecenum = turret2;
}

AimSecondary(heading, pitch)
{
	signal SIG_AIM_2;
	set-signal-mask SIG_AIM_2;
	turn turret2 to y-axis heading speed <125.000000>;
	turn canonB1 to x-axis <0.000000> - pitch speed <75.000000>;
	turn canonB2 to x-axis <0.000000> - pitch speed <75.000000>;
	wait-for-turn turret2 around y-axis;
	wait-for-turn canonB1 around x-axis;	
	start-script RestoreAfterDelay2();
	return (1);
}

FireSecondary()
{
	if( gun_2 == 0 )
	{
		emit-sfx 1025 from flareB2;
		//move canonB2 to z-axis [-1.000000] speed [200.000000];
		//wait-for-move canonB2 along z-axis;
		move barrelB2 to z-axis [-0.500000] speed [100.000000];
		wait-for-move barrelB2 along z-axis;
		move canonB2 to z-axis [0.000000] speed [10.000000];
		move barrelB2 to z-axis [0.000000] speed [10.000000];
	}
	if( gun_2 == 1 )
	{
		emit-sfx 1025 from flareB1;
		//move canonB1 to z-axis [-1.000000] speed [200.000000];
		//wait-for-move canonB1 along z-axis;
		move barrelB1 to z-axis [-0.500000] speed [100.000000];
		wait-for-move barrelB1 along z-axis;
		move canonB1 to z-axis [0.000000] speed [10.000000];
		move barrelB1 to z-axis [0.000000] speed [10.000000];
	}
	gun_2 = !gun_2;
}

QuerySecondary(piecenum)
{
	if( gun_2 == 0 )
	{
		piecenum = flareB2;
	}
	if( gun_2 == 1 )
	{
		piecenum = flareB1;
	}
}

SweetSpot(piecenum)
{
	piecenum = base;
}

Killed(severity, corpsetype)
{
	hide flare1;
	hide flareB1;
	hide flareB2;

	if( severity <= 25 )
	{
		corpsetype = 1;
		explode base type NOHEATCLOUD;
		explode sleeve1 type NOHEATCLOUD;
		explode turret1 type NOHEATCLOUD;
		explode turret2 type NOHEATCLOUD;
		return (1);
	}
	if( severity <= 50 )
	{
		corpsetype = 2;
		explode base type NOHEATCLOUD;
		explode sleeve1 type NOHEATCLOUD | FALL | EXPLODE_ON_HIT;
		explode turret1 type NOHEATCLOUD | SHATTER;
		explode turret2 type NOHEATCLOUD | SHATTER;
		return (1);
	}
	corpsetype = 3;
	explode base type NOHEATCLOUD;
	explode sleeve1 type NOHEATCLOUD | FALL | SMOKE | EXPLODE_ON_HIT;
	explode turret1 type NOHEATCLOUD | SHATTER;
	explode turret2 type NOHEATCLOUD | SHATTER;
	return (1);
}
